// Top-level build file where you can add configuration options common to all sub-projects/modules.
plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

ext {
    kotlin_version = '1.7.10'
    kotlin_coroutine_version = '1.6.4'
    annotation_version = '1.4.0'

    // Android
    corektx_version = '1.9.0'
    hilt_version = '2.43.2'

    // Test
    test_core_version = '1.4.0'
    assertj_version = '3.23.1'
    robolectric_version = '4.8.2'

    // Android project
    minSdk_version = 16
    compileSdk_version = 32
    targetSdk_version = 32
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

ext["signing.keyId"] = ''
ext["signing.password"] = ''
ext["signing.secretKeyRingFile"] = ''
ext["ossrhUsername"] = ''
ext["ossrhPassword"] = ''
ext["profileId"] = ''

File secretPropsFile = project.rootProject.file('local.properties')
if (secretPropsFile.exists()) {
    println "Found secret props file, loading props"
    Properties p = new Properties()
    p.load(new FileInputStream(secretPropsFile))
    p.each { name, value ->
        ext[name] = value
    }
} else {
    println "No props file, loading env vars"
    ext["signing.keyId"] = System.getenv('SIGNING_KEY_ID')
    ext["signing.password"] = System.getenv('SIGNING_PASSWORD')
    ext["signing.secretKeyRingFile"] = System.getenv('SIGNING_SECRET_KEY_RING_FILE')
    ext["ossrhUsername"] = System.getenv('MAVEN_USERNAME')
    ext["ossrhPassword"] = System.getenv('MAVEN_PASSWORD')
    ext["profileId"] = System.getenv('STAGING_PROFILE_ID')
}

nexusPublishing {
    packageGroup = rootProject.name
    repositories {
        sonatype {
            stagingProfileId = profileId
            username = ossrhUsername
            password = ossrhPassword
        }
    }
}
